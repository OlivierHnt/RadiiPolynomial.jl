"""
    LinearOperator{T<:VectorSpace,S<:VectorSpace,R<:AbstractMatrix}

Compactly supported linear operator with effective domain and codomain.

Fields:
- `domain :: T`
- `codomain :: S`
- `coefficients :: R`

Constructors:
- `LinearOperator(::VectorSpace, ::VectorSpace, ::AbstractMatrix)`
- `LinearOperator(coefficients::AbstractMatrix)`: equivalent to `LinearOperator(ParameterSpace()^size(coefficients, 2), ParameterSpace()^size(coefficients, 1), coefficients)`

# Examples

```jldoctest
julia> LinearOperator(Taylor(1), Taylor(1), [1 2 ; 3 4])
LinearOperator : Taylor(1) ‚Üí Taylor(1) with coefficients Matrix{Int64}:
 1  2
 3  4

julia> LinearOperator(Taylor(2), ParameterSpace(), [1.0 0.5 0.25])
LinearOperator : Taylor(2) ‚Üí ùïÇ with coefficients Matrix{Float64}:
 1.0  0.5  0.25

julia> LinearOperator([1 2 3 ; 4 5 6])
LinearOperator : ùïÇ¬≥ ‚Üí ùïÇ¬≤ with coefficients Matrix{Int64}:
 1  2  3
 4  5  6
```
"""
struct LinearOperator{T<:VectorSpace,S<:VectorSpace,R<:AbstractMatrix}
    domain :: T
    codomain :: S
    coefficients :: R
    function LinearOperator{T,S,R}(domain::T, codomain::S, coefficients::R) where {T<:VectorSpace,S<:VectorSpace,R<:AbstractMatrix}
        sz‚ÇÅ, sz‚ÇÇ = size(coefficients)
        (Base.OneTo(sz‚ÇÅ) == Base.axes(coefficients, 1)) & (Base.OneTo(sz‚ÇÇ) == Base.axes(coefficients, 2)) || return throw(ArgumentError("offset matrices are not supported"))
        dimension_domain = dimension(domain)
        dimension_codomain = dimension(codomain)
        (dimension_codomain == sz‚ÇÅ) & (dimension_domain == sz‚ÇÇ) || return throw(DimensionMismatch("dimensions must match: codomain and domain have dimensions $((dimension_codomain, dimension_domain)), coefficients has size $((sz‚ÇÅ, sz‚ÇÇ))"))
        return new{T,S,R}(domain, codomain, coefficients)
    end
end

LinearOperator(domain::T, codomain::S, coefficients::R) where {T<:VectorSpace,S<:VectorSpace,R<:AbstractMatrix} =
    LinearOperator{T,S,R}(domain, codomain, coefficients)

LinearOperator(coefficients::AbstractMatrix) =
    LinearOperator(ParameterSpace()^size(coefficients, 2), ParameterSpace()^size(coefficients, 1), coefficients)

Sequence(A::LinearOperator) = Sequence(codomain(A), vec(coefficients(A)))

domain(A::LinearOperator) = A.domain

codomain(A::LinearOperator) = A.codomain

coefficients(A::LinearOperator) = A.coefficients

# order, frequency

order(A::LinearOperator) = (order(domain(A)), order(codomain(A)))
order(A::LinearOperator, i::Int, j::Int) = (order(domain(A), j), order(codomain(A), i))

frequency(A::LinearOperator) = (frequency(domain(A)), frequency(codomain(A)))
frequency(A::LinearOperator, i::Int, j::Int) = (frequency(domain(A), j), frequency(codomain(A), i))

# utilities

function Base.firstindex(A::LinearOperator, i::Int)
    i == 1 && return _firstindex(codomain(A))
    i == 2 && return _firstindex(domain(A))
    return 1
end

function Base.lastindex(A::LinearOperator, i::Int)
    i == 1 && return _lastindex(codomain(A))
    i == 2 && return _lastindex(domain(A))
    return 1
end

Base.length(A::LinearOperator) = length(coefficients(A))

Base.size(A::LinearOperator) = size(coefficients(A))
Base.size(A::LinearOperator, i::Int) = size(coefficients(A), i)

Base.iterate(A::LinearOperator) = iterate(coefficients(A))
Base.iterate(A::LinearOperator, i) = iterate(coefficients(A), i)

Base.eltype(A::LinearOperator) = eltype(coefficients(A))
Base.eltype(::Type{<:LinearOperator{<:VectorSpace,<:VectorSpace,T}}) where {T<:AbstractMatrix} = eltype(T)

# getindex, view, setindex!

Base.@propagate_inbounds function Base.getindex(A::LinearOperator, Œ±, Œ≤)
    domain_A = domain(A)
    codomain_A = codomain(A)
    @boundscheck(
        (_checkbounds_indices(Œ±, codomain_A) & _checkbounds_indices(Œ≤, domain_A)) ||
        throw(BoundsError((indices(codomain_A), indices(domain_A)), (Œ±, Œ≤)))
        )
    return getindex(coefficients(A), _findposition(Œ±, codomain_A), _findposition(Œ≤, domain_A))
end

Base.@propagate_inbounds function Base.view(A::LinearOperator, Œ±, Œ≤)
    domain_A = domain(A)
    codomain_A = codomain(A)
    @boundscheck(
        (_checkbounds_indices(Œ±, codomain_A) & _checkbounds_indices(Œ≤, domain_A)) ||
        throw(BoundsError((indices(codomain_A), indices(domain_A)), (Œ±, Œ≤)))
        )
    return view(coefficients(A), _findposition(Œ±, codomain_A), _findposition(Œ≤, domain_A))
end

Base.@propagate_inbounds function Base.setindex!(A::LinearOperator, x, Œ±, Œ≤)
    domain_A = domain(A)
    codomain_A = codomain(A)
    @boundscheck(
        (_checkbounds_indices(Œ±, codomain_A) & _checkbounds_indices(Œ≤, domain_A)) ||
        throw(BoundsError((indices(codomain_A), indices(domain_A)), (Œ±, Œ≤)))
        )
    setindex!(coefficients(A), x, _findposition(Œ±, codomain_A), _findposition(Œ≤, domain_A))
    return A
end

# ==, iszero, isapprox

Base.:(==)(A::LinearOperator, B::LinearOperator) =
    codomain(A) == codomain(B) && domain(A) == domain(B) && all(t -> _safe_isequal(t...), zip(coefficients(A), coefficients(B)))

Base.iszero(A::LinearOperator) = _safe_iszero(coefficients(A))

Base.isapprox(A::LinearOperator, B::LinearOperator; kwargs...) =
    codomain(A) == codomain(B) && domain(A) == domain(B) && isapprox(coefficients(A), coefficients(B); kwargs...)

# copy, similar, zeros, ones, fill, fill!

Base.copy(A::LinearOperator) = LinearOperator(domain(A), codomain(A), copy(coefficients(A)))

Base.similar(A::LinearOperator) = LinearOperator(domain(A), codomain(A), similar(coefficients(A)))
Base.similar(A::LinearOperator, ::Type{T}) where {T} = LinearOperator(domain(A), codomain(A), similar(coefficients(A), T))

Base.zeros(s‚ÇÅ::VectorSpace, s‚ÇÇ::VectorSpace) = LinearOperator(s‚ÇÅ, s‚ÇÇ, zeros(dimension(s‚ÇÇ), dimension(s‚ÇÅ)))
Base.zeros(::Type{T}, s‚ÇÅ::VectorSpace, s‚ÇÇ::VectorSpace) where {T} = LinearOperator(s‚ÇÅ, s‚ÇÇ, zeros(T, dimension(s‚ÇÇ), dimension(s‚ÇÅ)))

Base.ones(s‚ÇÅ::VectorSpace, s‚ÇÇ::VectorSpace) = LinearOperator(s‚ÇÅ, s‚ÇÇ, ones(dimension(s‚ÇÇ), dimension(s‚ÇÅ)))
Base.ones(::Type{T}, s‚ÇÅ::VectorSpace, s‚ÇÇ::VectorSpace) where {T} = LinearOperator(s‚ÇÅ, s‚ÇÇ, ones(T, dimension(s‚ÇÇ), dimension(s‚ÇÅ)))

Base.fill(value, s‚ÇÅ::VectorSpace, s‚ÇÇ::VectorSpace) = LinearOperator(s‚ÇÅ, s‚ÇÇ, fill(value, dimension(s‚ÇÇ), dimension(s‚ÇÅ)))
function Base.fill!(A::LinearOperator, value)
    fill!(coefficients(A), value)
    return A
end

# zero, one

function Base.zero(A::LinearOperator)
    domain_A = domain(A)
    codomain_A = codomain(A)
    CoefType = eltype(A)
    C = LinearOperator(domain_A, codomain_A, Matrix{CoefType}(undef, dimension(codomain_A), dimension(domain_A)))
    coefficients(C) .= zero(CoefType)
    return C
end

function Base.one(A::LinearOperator)
    CoefType = eltype(A)
    C = zero(A)
    @inbounds for Œ± ‚àà indices(domain(A) ‚à© codomain(A))
        C[Œ±,Œ±] = one(CoefType)
    end
    return C
end

# float, complex, real, imag, conj, conj!

for f ‚àà (:float, :complex, :real, :imag, :conj, :conj!)
    @eval Base.$f(A::LinearOperator) = LinearOperator(domain(A), codomain(A), $f(coefficients(A)))
end

# promotion

Base.convert(::Type{LinearOperator{T‚ÇÅ,S‚ÇÅ,R‚ÇÅ}}, A::LinearOperator{T‚ÇÇ,S‚ÇÇ,R‚ÇÇ}) where {T‚ÇÅ,S‚ÇÅ,R‚ÇÅ,T‚ÇÇ,S‚ÇÇ,R‚ÇÇ} =
    LinearOperator{T‚ÇÅ,S‚ÇÅ,R‚ÇÅ}(convert(T‚ÇÅ, domain(A)), convert(S‚ÇÅ, codomain(A)), convert(R‚ÇÅ, coefficients(A)))

Base.promote_rule(::Type{LinearOperator{T‚ÇÅ,S‚ÇÅ,R‚ÇÅ}}, ::Type{LinearOperator{T‚ÇÇ,S‚ÇÇ,R‚ÇÇ}}) where {T‚ÇÅ,S‚ÇÅ,R‚ÇÅ,T‚ÇÇ,S‚ÇÇ,R‚ÇÇ} =
    LinearOperator{promote_type(T‚ÇÅ, T‚ÇÇ), promote_type(S‚ÇÅ, S‚ÇÇ), promote_type(R‚ÇÅ, R‚ÇÇ)}

# Cartesian spaces

Base.eachcol(A::LinearOperator{<:CartesianSpace,<:CartesianSpace}) =
    (@inbounds(component(A, :, j)) for j ‚àà Base.OneTo(nspaces(domain(A))))
Base.eachrow(A::LinearOperator{<:CartesianSpace,<:CartesianSpace}) =
    (@inbounds(component(A, i, :)) for i ‚àà Base.OneTo(nspaces(codomain(A))))

eachcomponent(A::LinearOperator{<:CartesianSpace,<:CartesianSpace}) =
    (@inbounds(component(A, i, j)) for i ‚àà Base.OneTo(nspaces(codomain(A))), j ‚àà Base.OneTo(nspaces(domain(A))))
eachcomponent(A::LinearOperator{<:CartesianSpace,<:VectorSpace}) =
    (@inbounds(component(A, j)) for i ‚àà Base.OneTo(1), j ‚àà Base.OneTo(nspaces(domain(A))))
eachcomponent(A::LinearOperator{<:VectorSpace,<:CartesianSpace}) =
    (@inbounds(component(A, i)) for i ‚àà Base.OneTo(nspaces(codomain(A))), j ‚àà Base.OneTo(1))

Base.@propagate_inbounds component(A::LinearOperator{<:CartesianSpace,<:CartesianSpace}, i, j) =
    LinearOperator(domain(A)[j], codomain(A)[i], view(coefficients(A), _component_findposition(i, codomain(A)), _component_findposition(j, domain(A))))

Base.@propagate_inbounds component(A::LinearOperator{<:CartesianSpace,<:VectorSpace}, j) =
    LinearOperator(domain(A)[j], codomain(A), view(coefficients(A), :, _component_findposition(j, domain(A))))

Base.@propagate_inbounds component(A::LinearOperator{<:VectorSpace,<:CartesianSpace}, i) =
    LinearOperator(domain(A), codomain(A)[i], view(coefficients(A), _component_findposition(i, codomain(A)), :))

Base.@propagate_inbounds function component(A::LinearOperator{<:CartesianSpace,<:CartesianSpace}, k::Any)
    n = nspaces(codomain(A))
    j, i = divrem(k, n)
    if iszero(i)
        return component(A, n, j)
    else
        return component(A, i, 1+j)
    end
end

# show

function Base.show(io::IO, ::MIME"text/plain", A::LinearOperator)
    println(io, "LinearOperator : ", _prettystring(domain(A)), " ‚Üí ", _prettystring(codomain(A)), " with coefficients ", typeof(coefficients(A)), ":")
    return Base.print_array(io, coefficients(A))
end

function Base.show(io::IO, A::LinearOperator)
    get(io, :compact, false) && return show(io, coefficients(A))
    return print(io, "LinearOperator(", domain(A), ", ", codomain(A), ", ", coefficients(A), ")")
end
